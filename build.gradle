import com.modrinth.minotaur.dependencies.ModDependency

plugins {
    id 'fabric-loom'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm'
    id 'com.matthewprenger.cursegradle'
    id 'com.modrinth.minotaur'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
    accessWidenerPath = file("src/main/resources/magical-mahou.accesswidener")
}

repositories {
    maven { url = "https://maven.fabricmc.net/" }
    maven {
        // LBA, LNS, and SimplePipes
        name = "BuildCraft"
        url = "https://mod-buildcraft.com/maven"
    }
    maven {
        // LibGUI
        name = "CottonMC"
        url = "https://server.bbkr.space/artifactory/libs-release"
        content {
            includeGroup 'io.github.cottonmc'
        }
    }
    maven {
        name = 'Ladysnake Mods'
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
    }
    if (isTerraformersMCMavenUp()) {
        maven {
            // Mod Menu
            name = "TerraformersMC"
            url = "https://maven.terraformersmc.com/releases/"
            content {
                includeGroup 'com.terraformersmc'
            }
        }
    }
    mavenCentral()
    maven {
        // where Grondag's mods live
        name = "dblsaiko"
        url = "https://maven.dblsaiko.net/"
    }
    maven {
        // Cloth Config (required by Canvas)
        name = "shedaniel"
        url = "https://maven.shedaniel.me/"
    }
    maven {
        // REI, odds and ends
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven"
    }
    maven {
        // Myron OBJ loader
        name = "Haven-King"
        url = "https://hephaestus.dev/release"
    }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"


    // Non-Fabric dependencies

    // Api dependencies

    modApi("io.github.cottonmc:LibGui:${project.libgui_version}") {
        exclude group: 'net.fabricmc.fabric-api'
    }
    include "io.github.cottonmc:LibGui:${project.libgui_version}"

    // Implementation dependencies

    modImplementation("io.github.ladysnake:satin:${project.satin_version}") {
        exclude group: 'net.fabricmc.fabric-api'
    }
    include "io.github.ladysnake:satin:${project.satin_version}"

    def cardinalComponents = ["cardinal-components-base", "cardinal-components-entity"]
    cardinalComponents.each { component ->
        modImplementation("dev.onyxstudios.cardinal-components-api:${component}:${project.cardinal_version}") {
            exclude group: 'net.fabricmc.fabric-api'
        }
        include "dev.onyxstudios.cardinal-components-api:${component}:${project.cardinal_version}"
    }

    modImplementation("dev.monarkhes:myron:${project.myron_version}") {
        exclude group: 'net.fabricmc.fabric-api'
    }
    include "dev.monarkhes:myron:${project.myron_version}"

    modImplementation("alexiil.mc.lib:libnetworkstack-base:${project.lns_version}") {
        exclude group: 'net.fabricmc.fabric-api'
    }
    include "alexiil.mc.lib:libnetworkstack-base:${project.lns_version}"


    // Runtime dependencies (compatibility testing mostly)

    if (isTerraformersMCMavenUp()) {
        modRuntimeOnly("com.terraformersmc:modmenu:${project.modmenu_version}") {
            exclude group: "net.fabricmc.fabric-api"
        }
    }

    modRuntimeOnly("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }
    modRuntimeOnly("dev.architectury:architectury-fabric:${project.architectury_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }

    compileOnly "com.google.code.findbugs:jsr305:${project.jsr305_version}"
    compileOnly "com.demonwav.mcdev:annotations:${project.mcdev_annotations_version}"
}

processResources {
    inputs.property "version", project.version

    exclude("**/*.xcf")

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"

    // Minecraft 1.17 (21w19a) upwards uses Java 16.
    it.options.release = 16
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

afterEvaluate {
    runClient {
        args("--width", "1280", "--height", "720")
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            groupId = project.maven_group
            artifactId = project.name
            version = project.mod_version

            from(components['java'])
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}

compileKotlin.kotlinOptions.jvmTarget = "16"

if (System.getenv("CURSE_API_KEY") != null) {
    curseforge {
        apiKey = System.getenv("CURSE_API_KEY")
        project {
            id = '499532'
            changelogType = 'markdown'
            changelog = file("changelogs/changelog-${project.mod_version}.md")
            releaseType = project.mod_release_type
            project.publish_minecraft_versions.split(",").toList().forEach {
                addGameVersion it.trim()
            }
            addGameVersion 'Java 16'
            addGameVersion 'Fabric'
            mainArtifact(remapJar)
            relations {
                requiredDependency 'fabric-api'
                requiredDependency 'fabric-language-kotlin'
            }
        }
        options {
            forgeGradleIntegration = false
        }
    }

    afterEvaluate {
        tasks.curseforge499532.dependsOn remapJar
    }
}

modrinth {
    token = System.getenv("MODRINTH_API_KEY")
    projectId = 'CqZmZ0Hh'
    versionNumber = project.mod_version
    versionType = project.mod_release_type
    File changelogFile = rootProject.file("changelogs/changelog-${mod_version}.md")
    if (changelogFile.exists()) {
        changelog = changelogFile.text
    }
    uploadFile = remapJar
    additionalFiles = [sourcesJar]
    gameVersions = Arrays.asList(project.publish_minecraft_versions.split("\\s*,\\s*"))
    loaders = ['fabric']
    dependencies = [
            new ModDependency('P7dR8mSH', 'required'), // fabric api
            new ModDependency('Ha28R6CL', 'required')  // fabric language kotlin
    ]
    syncBodyFrom = rootProject.file("README.md").text
}

static boolean isTerraformersMCMavenUp() {
    try {
        URL url = new URL("https://maven.terraformersmc.com/releases/com/terraformersmc/modmenu/")
        HttpURLConnection connection = (HttpURLConnection) url.openConnection()
        connection.setRequestMethod("GET")
        connection.connect()

        int responseClass = connection.getResponseCode().intdiv(100)
        return responseClass != 4 && responseClass != 5
    } catch (IOException e) {
        println("Error connecting to TerraformersMC maven", e)
    }
    return false
}
